group 'io.greennav'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'java-library-distribution'
apply plugin: 'idea'
apply plugin: 'com.google.protobuf'

sourceCompatibility = 1.8
targetCompatibility = 1.8
def sparkVersion = '2.1.0'

repositories {
    maven { url 'http://repo1.maven.org/maven2/' }
    maven { url 'http://mvn.topobyte.de' }
}

buildscript {
    repositories {
        maven { url 'http://repo1.maven.org/maven2/' }
        maven { url 'http://mvn.topobyte.de' }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.1'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile 'com.google.protobuf:protobuf-java:3.0.0'
    compile 'io.grpc:grpc-stub:1.0.0-pre2'
    compile 'io.grpc:grpc-protobuf:1.0.0-pre2'

    compile group: 'org.apache.spark', name: 'spark-sql_2.10', version: sparkVersion
    compile group: 'org.apache.parquet', name: 'parquet-avro', version: '1.9.0'
    compile group: 'org.apache.avro', name: 'avro', version: '1.8.2'

    compile 'com.databricks:spark-avro_2.10:3.2.0'

    compile group: 'org.postgresql', name: 'postgresql', version: '42.0.0'
    compile group: 'net.postgis', name: 'postgis-jdbc', version: '2.2.1'

    compile group: 'log4j', name: 'log4j', version: '1.2.16'

    compile('de.topobyte:osm4j-core:0.0.20') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'log4j', module: 'log4j'
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    generateProtoTasks.generatedFilesBaseDir = 'src'
}

task fatJar(type: Jar) {
    baseName = project.name + '-all'
    manifest { attributes 'Main-Class': 'io.greennav.persistence.DatabaseApplication'}
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
    zip64 = true
    with jar
}